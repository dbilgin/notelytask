plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keyPropertiesFile = file('key.properties')
def keyProperties = new Properties()
if (keyPropertiesFile.exists()) {
    keyProperties.load(new FileInputStream(keyPropertiesFile))
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '84'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '4.0.0'
}

android {
    namespace "com.omedacore.notelytask"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.omedacore.notelytask"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        // Debug config for local development
        if (keyProperties.getProperty('DEBUG_STORE_FILE') != null) {
            debug {
                storeFile file(keyProperties.getProperty('DEBUG_STORE_FILE'))
                storePassword keyProperties.getProperty('DEBUG_STORE_PASSWORD')
                keyAlias keyProperties.getProperty('DEBUG_KEY_ALIAS')
                keyPassword keyProperties.getProperty('DEBUG_KEY_PASSWORD')
            }
        }
        // Release config for CI builds
        if (keyProperties.getProperty('RELEASE_STORE_FILE') != null) {
            release {
                storeFile file(keyProperties.getProperty('RELEASE_STORE_FILE'))
                storePassword keyProperties.getProperty('RELEASE_STORE_PASSWORD')
                keyAlias keyProperties.getProperty('RELEASE_KEY_ALIAS')
                keyPassword keyProperties.getProperty('RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            if (keyProperties.getProperty('RELEASE_STORE_FILE') != null) {
                signingConfig signingConfigs.release
            } else if (keyProperties.getProperty('DEBUG_STORE_FILE') != null) {
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {}
